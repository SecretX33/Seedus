plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = "io.github.secretx33"
version = "1.1"

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "gradle-repo"
        url = "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT"
    compileOnly "org.jetbrains:annotations:23.0.0"
    compileOnly "com.google.guava:guava:30.1-jre"
    compileOnly "org.checkerframework:checker-qual:3.23.0"
    implementation "com.github.stephanenicolas.toothpick:toothpick-runtime:3.1.0"
    annotationProcessor "com.github.stephanenicolas.toothpick:toothpick-compiler:3.1.0"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

// Disables the normal jar task
jar.enabled = false

// And enables shadowJar task
artifacts { archives shadowJar }

shadowJar {
    archiveFileName = "${rootProject.name}.jar"
    var dependencyPackage = "${rootProject.group}.dependencies.${rootProject.name.toLowerCase()}"
    relocate("javax.inject", "${dependencyPackage}.javax.inject")
    relocate("toothpick", "${dependencyPackage}.toothpick")
    from("LICENSE").rename("(LICENSE)", '$1.txt')
}

processResources {
    outputs.upToDateWhen { false }
    var mainClass = "${project.group}.${project.name.toLowerCase()}.${project.name}"
    expand([
        version: version,
        name: rootProject.name,
        mainClass: mainClass,
    ])
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }
}
